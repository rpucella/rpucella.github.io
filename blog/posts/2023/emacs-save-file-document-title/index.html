<!DOCTYPE html>
<html>

  <head>
    
    <meta charset="utf-8">
    <title>Riccardo Pucella</title>

    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link rel="stylesheet" href="/static/main.css" type="text/css">
    
    <link href="https://fonts.googleapis.com/css2?family=Montserrat:ital,wght@0,400;0,700;1,400;1,700&display=swap" rel="stylesheet">
    <link href="https://fonts.googleapis.com/css2?family=Courier+Prime:ital,wght@0,400;0,700;1,400;1,700&display=swap" rel="stylesheet">
    <link
      href="https://fonts.googleapis.com/css2?family=Lora:ital,wght@0,400;0,700;1,400;1,700&display=swap"
      rel="stylesheet">
  </head>

  <body>
  
    <nav>

      

      <input type="checkbox" />

      <span></span>
      <span></span>
      <span></span>

      <ul>
	<li id="home"><a href="/">Riccardo Pucella</a></li>
	<li id="teaching"><a href="/courses">Teaching</a></li>
	<li id="writing"><a href="/papers">Writing</a></li>
 	<li id="blogging"><a href="/blog">Blogging</a></li>
      </ul>

    </nav>

    <script>
  document.title = "Saving a File Under the Document Title Name in Emacs" + ' â€” Riccardo Pucella'
  document.querySelector("#blogging").classList.add("active")
</script>

<main>
  
  <article class="post">
  
    <h1 class="title">Saving a File Under the Document Title Name in Emacs</h3>
    
    <div class="date">May 14, 2023</div>
    
    <div class="body">
      <p>I write and code in <a href="https://www.gnu.org/software/emacs/">Emacs</a>. It's too late for me now to change the muscle memory I've acquired over a
lifetime. Moreover, now that I've started to work in <a href="https://www.gnu.org/software/emacs/manual/eintr.html">Emacs Lisp</a> more seriously, I have much of my
workflow directly embedded in the editor, a few keystrokes away.</p>

<p>For example, here's a useful routine for saving a buffer under the same title as the document
contained in that buffer, even if the title of the document changes during editing. Because this
code was written to handle <a href="https://daringfireball.net/projects/markdown/">Markdown</a> documents, the title of a document here is taken to be the first
non-empty line of the document when it starts with a <code>#</code>. Every time the document is saved, the buffer
and the file it visits is automatically renamed to the new title before saving. The original file
extension is kept. All the magic is encapsulated within the <a href="https://www.gnu.org/software/emacs/manual/html_node/elisp/Saving-Buffers.html"><code>before-save-hook</code></a>, which gets invoked
when a buffer is about to be saved to a file.</p>

<p>To enable this functionality in a buffer visiting a (Markdown) document, simply hit <code>M-x track-title-change</code>.</p>

<pre><code>(defun buffer-document-title ()
  &quot;Return document title as defined by # &lt;title&gt;
on the first non-empty line in the buffer.&quot;
  (let ((line nil)
        (found nil)
        (title-regexp (rx string-start
                          (one-or-more &quot;#&quot;)
                          (one-or-more space)
                          (group (zero-or-more not-newline))
                          string-end)))
    (save-excursion
      (goto-char (point-min))
      (while (and (not found) (not (eobp)))
        (setq line (string-trim (buffer-substring-no-properties
                                 (line-beginning-position)
                                 (line-end-position))))
        (when (not (string-empty-p line))
          (setq found t))
        (forward-line 1))
      (and found
           (string-match title-regexp line)
           (match-string 1 line)))))

(defun rename-current-buffer-file-to-title ()
  &quot;Rename current buffer and file it is visiting to the
title of the file (as determined by buffer-document-title).&quot;
  (let* ((title (buffer-document-title))
         (old-name (buffer-file-name))
         (directory nil)
         (extension nil)
         (new-name nil))
    (when (and old-name title)
      (setq directory (file-name-directory old-name))
      (setq extension (file-name-extension old-name))
      (setq new-name (concat directory title &quot;.&quot; extension))
      (when (not (equal new-name old-name))
        ;; Rename buffer + saved file name.
        (rename-current-buffer-file new-name)))))

(defun rename-current-buffer-file (new-name)
  &quot;Renames current buffer and file it is visiting.&quot;
  (let ((name (buffer-name))
        (filename (buffer-file-name)))
    (when (not (and filename (file-exists-p filename)))
      (error &quot;Buffer '%s' is not visiting a file!&quot; name))
    (when (get-buffer new-name)
      (error &quot;A buffer named '%s' already exists!&quot; new-name))
    (rename-file filename new-name 1)
    (rename-buffer new-name)
    (set-visited-file-name new-name)
    (set-buffer-modified-p nil)
    (message &quot;File '%s' successfully renamed to '%s'.&quot;
             name (file-name-nondirectory new-name))))

(defun track-title-change ()
  &quot;Track the title of a buffer and use the title as filename when saving.&quot;
  (interactive)
  (let ((filename (buffer-file-name)))
    (when (not (and filename (file-exists-p filename)))
      (error &quot;Buffer '%s' is not visiting a file!&quot; name))
    (add-hook 'before-save-hook 'rename-current-buffer-file-to-title nil t)))
</code></pre>

<p>Function <code>rename-current-buffer-file</code> in the code above is taken directly from <a href="http://whattheemacsd.com/file-defuns.el-01.html">What the .emacs.d</a>.</p>

<p>Though it was written to handle Markdown documents, there is nothing particularly Markdown-specific
in this code. If you want to apply this other kind of documents, you only need to redefine
<code>buffer-document-title</code> to identify what you consider the title of the document. Of course, a more robust
implementation would allow you to have different ways to determine the document title based on the
major mode of the buffer. Let me leave that as an exercise to the reader.</p>

    </div>

    
      <div class="reading">A Maggot (by John Fowles)</div>
    

     <div class="human">This post was written entirely by a human.</div>

  </article>
  
</main>

    
  </body>
  
</html>
